package require tcldbf
package require tcltest
namespace import ::tcltest::*

# encoding system utf-8

testConstraint utf8 [expr {[encoding system] eq "utf-8"}]

###

set simple_struct {
   {F1 Logical L 1 0}
   {F2 Date D 8 0}
   {F3 String C 10 0}
   {F4 Integer N 9 0}
   {F5 Double N 10 2}
}

set simple_data {
   {T 20241231 {New Year 0} 1230 456.7}
   {F 20250101 {New Year 1} 1231 456.71}
}

proc dbf_create {filename struct args} {
   dbf dbf create $filename {*}$args
   foreach l $struct {
       lassign $l l - t w p
       $dbf add $l $t $w $p
   }
   $dbf forget
}

proc dbf_create_open {filename struct args} {
   dbf_create $filename $struct {*}$args
   dbf dbf open $filename
}

proc dbf_insert {dbf data} {
   foreach l $data {
      $dbf insert end {*}$l
   }
}

###

foreach {n c} {
   0 "dbf"
   1 "dbf d"
   2 "dbf d foo" 
   3 "dbf d create" 
   4 "dbf d open" 
   5 "dbf d foo bar" 
} {
   test dbf-1.0.$n "usage '$c'" -returnCodes 1 -body {
      {*}$c
   } -result {wrong # args: should be "dbf <varname> create|open <filename> ?option?"}
   unset n c
}

# create / open errors

test dbf-1.0.10 "usage 'dbf create with opts'" -returnCodes 1 -body {
   dbf d create foo -codepage
} -result {wrong # args: should be "dbf <varname> create <filename> ?-codepage <codepage>?"}

test dbf-1.0.11 "usage 'dbf create with opts2'" -returnCodes 1 -body {
   dbf d create foo -bar baz
} -result {wrong # args: should be "dbf <varname> create <filename> ?-codepage <codepage>?"}

test dbf-1.0.12 "usage 'dbf open with opts'" -returnCodes 1 -body {
   dbf d open foo -bar
} -result {wrong # args: should be "dbf <varname> open <filename> ?-readonly?"}

test dbf-1.0.13 "create invalid name" -returnCodes 1 -body {
   dbf d create __notexistent__/foo
} -result {create __notexistent__/foo failed}

test dbf-1.0.14 "create invalid name (v1 compat)" -body {
   dbf d -create __notexistent__/foo
} -result {0}

test dbf-1.0.15 "open invalid name" -returnCodes 1 -body {
   dbf d open __notexistent__/foo
} -result {open __notexistent__/foo failed}

test dbf-1.0.16 "open invalid name (v1 compat)" -body {
   dbf d -open __notexistent__/foo
} -result {0}

test dbf-1.0.17 "open invalid file" -returnCodes 1 -setup {
   set f [tcltest::makeFile "bar" foo]
} -cleanup {
   tcltest::removeFile foo; unset f
} -body {
   dbf d open $f
} -result {open */foo failed} -match glob

test dbf-1.0.18 "open invalid file (v1 compat)" -setup {
   set f [tcltest::makeFile "bar" foo]
} -cleanup {
   tcltest::removeFile foo; unset f
} -body {
   dbf d -open $f
} -result {0}

# create

test dbf-2.0.0.0 {create} -cleanup {
   catch {rename $d ""; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf d create [file join [temporaryDirectory] test.dbf]
} -result {dbf.????} -match glob

test dbf-2.0.0.1 {create (v1 compat)} -cleanup {
   catch {rename $d ""; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf d -create [file join [temporaryDirectory] test.dbf]
} -result 1 -match glob

test dbf-2.0.1 {create/command} -cleanup {
   catch {rename $d ""; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf d create [file join [temporaryDirectory] test.dbf]
   info commands $d
} -result {dbf.????} -match glob

test dbf-2.0.2 {create/forget} -cleanup {
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf d create [file join [temporaryDirectory] test.dbf]
   $d forget
   info commands $d
} -result {}

# open

test dbf-3.0.0.0 {create/open} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf d open [file join [temporaryDirectory] test.dbf]
} -result {dbf.????} -match glob

test dbf-3.0.0.1 {create/open compat} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -result 1

test dbf-3.0.2 {create/open/command} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf d -open [file join [temporaryDirectory] test.dbf]
   info commands $d
} -result {dbf.????} -match glob

test dbf-3.0.3 {create/open/forget} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
} -cleanup {
   catch {unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf d open [file join [temporaryDirectory] test.dbf]
   $d forget
   info commands $d
} -result {}

# open with codepage

test dbf-3.1.0 {create/open/codepage default} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d codepage
} -result {LDID/87}

test dbf-3.1.1 {create/open/codepage custom} -setup {
   dbf d create [file join [temporaryDirectory] test.dbf] -codepage "LDID/201"
   $d forget; unset d
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d codepage
} -result {LDID/201}

# add fields

test dbf-3.2.0.0 {create/setup error} -returnCodes 1 -setup {
   dbf d create [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d add X X 1
} -result {invalid type, field X}

test dbf-3.2.0.1 {create/setup error} -returnCodes 1 -setup {
   dbf d create [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d add S C 256
} -result {invalid width, field S}

test dbf-3.2.0.2 {create/setup error} -returnCodes 1 -setup {
   dbf d create [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   for {set i 0} {$i <= 256} {incr i} {
      $d add S$i C 255
   }
} -result {Cannot add field S256. Record length limit reached (max 65535 bytes).}

test dbf-3.2.1 {create/setup} -setup {
   dbf d create [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list \
         [$d add F1 L 1] \
         [$d add F2 D 8] \
         [$d add F3 C 10] \
         [$d add F4 N 10] \
         [$d add F5 N 10 2]
} -result {0 1 2 3 4}

test dbf-3.2.2 {create/setup/open/field} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] $simple_struct
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   unset -nocomplain f l
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   set l {}
   foreach f $simple_struct {
       lappend l [$d fields [lindex $f 0]]
   }
   set l
} -result [lrange $simple_struct 0 end]

test dbf-3.2.3 {create/setup/open/fields} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] $simple_struct
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d fields
} -result [lrange $simple_struct 0 end]

test dbf-3.2.4 {create/setup/open/info} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] $simple_struct
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d info
} -result [list 0 [llength $simple_struct]]

# insert

test dbf-4.0.0 {create/setup/open/insert past end} -returnCodes 1 -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d insert 1 {foo}
} -result {invalid rowid 1}

test dbf-4.0.1 {create/setup/open/insert many values} -returnCodes 1 -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d insert end {foo} {bar}
} -result {too many values}

test dbf-4.0.2 {create/setup/open/insert invalid number} -returnCodes 1 -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{I - N 9 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d insert end {foo}
} -result {expected integer but got "foo", field I}

test dbf-4.0.3 {create/setup/open/insert invalid number} -returnCodes 1 -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{F - N 10 2}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d insert end {foo}
} -result {expected floating-point number but got "foo", field F}

test dbf-4.0.4 {create/setup/open/insert invalid date} -returnCodes 1 -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{D - D 8 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d insert end {foo}
} -result {expected date as YYYYMMDD but got "foo", field D}

test dbf-4.0.5 {create/setup/open/insert invalid logical} -returnCodes 1 -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{L - L 1 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d insert end X
} -result {update error, field L}

#
# NOTE: Insert error, but record added
#
test dbf-4.0.6 {create/setup/open/insert invalid logical/record count} -constraints shapelibFeature -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{L - L 1 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d info] [catch {$d insert end X}] [$d info]
} -result {{0 1} 1 {0 1}}

#
# NOTE: Two assetions (msvc debug): "Flush between consecutive read and write", !stream.has_any_of(_IOREAD)
# NOTE: No error at Tcl level
# NOTE: Record not added 
# 
test dbf-4.0.8 {create/setup/open ro/insert} -returnCodes 1 -constraints shapelibFeature -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf] -readonly
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert end {foo}] [$d info]
} -result {Failure writing DBF record 0.}

test dbf-4.1.0 {create/setup/open/insert} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert end {foo}] [$d info]
} -result {0 {1 1}}

test dbf-4.1.1 {create/setup/open/insert twice} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert end {foo}] [$d insert end {bar}] [$d info]
} -result {0 1 {2 1}}

test dbf-4.1.2 {create/setup/open/insert twice same rowid} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert 0 {foo}] [$d insert 0 {bar}] [$d info]
} -result {0 0 {1 1}}

# update

test dbf-4.2.1 {create/setup/open/update} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d update end S {foo}] [$d info]
} -result {0 {1 1}}

test dbf-4.2.2 {create/setup/open/update twice} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d update end S {foo}] [$d update end S {bar}] [$d info]
} -result {0 1 {2 1}}

# record

test dbf-4.3.0 {create/setup/open/insert/record} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert 0 {foo}] [$d record 0]
} -result {0 foo}

test dbf-4.3.1 {create/setup/open/too long insert} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert end {foofoofoofoo}] [$d record 0]
} -result {0 foofoofoof}

test dbf-4.3.2 {create/setup/open/update/record} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d update 0 S {foo}] [$d record 0]
} -result {0 foo}

test dbf-4.3.3 {create/setup/open/insert two strings} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S1 - C 10 0} {S2 - C 10 0}}
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert 0 foo bar] [$d record 0]
} -result {0 {foo bar}}

test dbf-4.3.4 {insert a string with two words} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S1 - C 10 0} {S2 - C 10 0}}
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert 0 {foo bar}] [$d record 0]
} -result {0 {{foo bar} {}}}

test dbf-4.3.5 {insert a list with two strings} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S1 - C 10 0} {S2 - C 10 0}}
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert 0 [list foo bar]] [$d record 0]
} -result {0 {foo bar}}

test dbf-4.3.6 {insert list and strings} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S1 - C 10 0} {S2 - C 10 0}}
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert 0 [list foo bar] baz] [$d record 0]
} -result {0 {{foo bar} baz}}

test dbf-4.3.7 {empty list} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d insert 0 {}] [$d info]
} -result {0 {0 1}}

test dbf-4.3.8 {insert long record} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] $simple_struct
   dbf d open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   dbf_insert $d $simple_data
   list [$d record 0] [$d record 1]
} -result [lrange $simple_data 0 end]

# get

test dbf-4.4.0 {get syntax} -returnCodes 1 -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S1 - C 10 0} {S2 - C 10 0}}
   dbf d -open [file join [temporaryDirectory] test.dbf]
   $d insert 0 foo bar
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d get 0 S1 S2
} -result {wrong # args: should be "<dbf> get <rowid> ?label?"}

test dbf-4.4.1 {get invalid field} -returnCodes 1 -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S1 - C 10 0} {S2 - C 10 0}}
   dbf d -open [file join [temporaryDirectory] test.dbf]
   $d insert 0 foo bar
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d get 0 S3
} -result {unknown field S3}

test dbf-4.4.2 {get by field label} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S1 - C 10 0} {S2 - C 10 0}}
   dbf d -open [file join [temporaryDirectory] test.dbf]
   $d insert 0 foo bar
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d get 0 S1] [$d get 0 S2]
} -result {foo bar}

test dbf-4.4.3 {get dict} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S1 - C 10 0} {S2 - C 10 0}}
   dbf d -open [file join [temporaryDirectory] test.dbf]
   $d insert 0 foo bar
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d get 0
} -result {S1 foo S2 bar}

# values

test dbf-4.5.0 {values} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
   $d insert end {foo}
   $d insert end {bar}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   $d values S
} -result {foo bar}

# deleted

test dbf-4.6.0 {deleted mark} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {{S - C 10 0}}
   dbf d open [file join [temporaryDirectory] test.dbf]
   $d insert 0 {foo}
} -cleanup {
   catch {$d forget; unset d}
   #catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d deleted 0] [$d deleted 0 true] [$d deleted 0] [$d deleted 0 false] [$d deleted 0]
} -result {0 1 1 0 0}

# codepage encoding

test dbf-4.7.0 {codepage} -setup {
   dbf d create [file join [temporaryDirectory] test.dbf] -codepage LDID/201
   $d add S C 10
   $d insert 0 {Йцукенгшщз}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d codepage] [$d record 0]
} -result {LDID/201 Йцукенгшщз}

test dbf-4.7.1 {codepage/truncation} -setup {
   dbf d create [file join [temporaryDirectory] test.dbf] -codepage LDID/201
   $d add S C 10
   $d insert 0 {Йцукенгшщзх}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d codepage] [$d record 0]
} -result {LDID/201 Йцукенгшщз}

test dbf-4.7.2 {codepage/utf} -constraints utf8 -setup {
   dbf d create [file join [temporaryDirectory] test.dbf]
   $d add S C 10
   $d insert 0 {Йцуке}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d codepage] [$d record 0]
} -result {LDID/87 Йцуке}

test dbf-4.7.3 {codepage/utf truncated} -constraints utf8 -setup {
   dbf d create [file join [temporaryDirectory] test.dbf]
   $d add S C 10
   $d insert 0 {Йцукен}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d codepage] [$d record 0]
} -result {LDID/87 Йцуке}

test dbf-4.7.4 {codepage/utf truncated incompletely} -constraints utf8 -setup {
   dbf d create [file join [temporaryDirectory] test.dbf]
   $d add S C 11
   $d insert 0 {Йцукен}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d codepage] [$d record 0]
} -result "LDID/87 Йцуке\xD0"

test dbf-4.7.5 {codepage after open} -setup {
   dbf d create [file join [temporaryDirectory] test.dbf] -codepage LDID/201
   $d add S C 10
   $d insert 0 {Йцукенгшщз}
   $d forget
   dbf d open [file join [temporaryDirectory] test.dbf]
   $d insert 1 {Йцукенгшщз}
} -cleanup {
   catch {$d forget; unset d}
   catch {file delete [file join [temporaryDirectory] test.dbf]}
} -body {
   list [$d codepage] [$d record 0] [$d record 1]
} -result {LDID/201 Йцукенгшщз Йцукенгшщз}

cleanupTests
