package require tcldbf 2.0.1-
package require tcltest
namespace import ::tcltest::*

testConstraint dbasefiles [file exist [file join [testsDirectory] dbasefiles t1 With_data T1.DBF]]
testConstraint fixtures [file exist [file join [testsDirectory] fixtures cp1251.dbf]]

proc dbf_create {filename struct args} {
   dbf dbf -create $filename {*}$args
   foreach l $struct {
       lassign $l l - t w p
       $dbf add $l $t $w $p
   }
   $dbf forget
}

# syntax

foreach {n c} {
   0 "dbf"
   1 "dbf d"
   2 "dbf d -foo" 
   3 "dbf d -create" 
   4 "dbf d -open" 
} {
   test dbf-1.$n "usage '$c'" -returnCodes 1 -body {
      {*}$c
   } -result {wrong # args: should be "dbf <varname> -create|-open <filename> ?option?"}
   unset n c
}

test dbf-1.10 "usage 'dbf create with opts'" -returnCodes 1 -body {
   dbf d -foo bar
} -result {wrong # args: should be "dbf d -create|-open <filename> ?option?"}

test dbf-1.11 "usage 'dbf create with opts'" -returnCodes 1 -body {
   dbf d -create foo -codepage
} -result {wrong # args: should be "dbf d -create <filename> ?-codepage <codepage>?"}

test dbf-1.12 "usage 'dbf create with opts2'" -returnCodes 1 -body {
   dbf d -create foo -bar baz
} -result {wrong # args: should be "dbf d -create <filename> ?-codepage <codepage>?"}

test dbf-1.13 "usage 'dbf open with opts'" -returnCodes 1 -body {
   dbf d -open foo -bar
} -result {wrong # args: should be "dbf d -open <filename> ?-readonly?"}

# create / open errors

test dbf-2.1 "create invalid name" -returnCodes 1 -body {
   dbf d -create __notexistent__/foo
} -result {create __notexistent__/foo failed}

test dbf-2.2 "open invalid name" -returnCodes 1 -body {
   dbf d -open __notexistent__/foo
} -result {open __notexistent__/foo failed}

test dbf-2.3 "open invalid file" -returnCodes 1 -setup {
   set f [tcltest::makeFile "bar" foo]
} -cleanup {
   tcltest::removeFile foo; unset f
} -body {
   dbf d -open $f
} -result {open */foo failed} -match glob

# create

test dbf-3.0 {create} -cleanup {
   rename $d ""; unset d; file delete [file join [temporaryDirectory] test.dbf]
} -body {
   dbf d -create [file join [temporaryDirectory] test.dbf]
} -result {dbf.????} -match glob

test dbf-3.1 {create/command} -cleanup {
   rename $d ""; unset d; file delete [file join [temporaryDirectory] test.dbf]
} -body {
   dbf d -create [file join [temporaryDirectory] test.dbf]
   info commands $d
} -result {dbf.????} -match glob

test dbf-4.2 {create/forget} -cleanup {
   unset d; file delete [file join [temporaryDirectory] test.dbf]
} -body {
   dbf d -create [file join [temporaryDirectory] test.dbf]
   $d forget
   info commands $d
} -result {}

# open

test dbf-5.1 {create/open} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
} -cleanup {
   $d forget; unset d; file delete [file join [temporaryDirectory] test.dbf]
} -body {
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -result {dbf.????} -match glob

test dbf-5.2 {create/open/command} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
} -cleanup {
   $d forget; unset d; file delete [file join [temporaryDirectory] test.dbf]
} -body {
   dbf d -open [file join [temporaryDirectory] test.dbf]
   info commands $d
} -result {dbf.????} -match glob

test dbf-5.3 {create/open/forget} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
} -cleanup {
   unset d; file delete [file join [temporaryDirectory] test.dbf]
} -body {
   dbf d -open [file join [temporaryDirectory] test.dbf]
   $d forget
   info commands $d
} -result {}

# open with codepage

test dbf-6.0 {create/open/codepage default} -setup {
   dbf_create [file join [temporaryDirectory] test.dbf] {}
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   $d forget; unset d; file delete [file join [temporaryDirectory] test.dbf]
} -body {
   $d codepage
} -result {LDID/87}

test dbf-6.1 {create/open/codepage custom} -setup {
   dbf d -create [file join [temporaryDirectory] test.dbf] -codepage "LDID/201"
   $d forget; unset d
   dbf d -open [file join [temporaryDirectory] test.dbf]
} -cleanup {
   $d forget; unset d; file delete [file join [temporaryDirectory] test.dbf]
} -body {
   $d codepage
} -result {LDID/201}

# nondefault extension

test dbf-7.1 {create with custom ext} -setup {
   dbf d -create [file join [temporaryDirectory] test.foo]
   $d forget; unset d
} -cleanup {
   file delete [file join [temporaryDirectory] test.foo]
} -body {
   file exists [file join [temporaryDirectory] test.foo]
} -result {1}

test dbf-7.2 {create/open with custom ext} -setup {
   dbf d -create [file join [temporaryDirectory] test.foo]
   $d forget; unset d
} -cleanup {
   $d forget; unset d; file delete [file join [temporaryDirectory] test.foo]
} -body {
   dbf d -open [file join [temporaryDirectory] test.foo]
} -result {dbf.????} -match glob

# open external samlpes

foreach {f p} {
    t1 With_data/T1 
    t2 With_data/T2 
    t3 with_data/t3 
    t4 with_data/T4
    t5 with_data/T5
    t6 with_data/t6
} {
    test dbf-10.dbasefiles/$f "open dbf dbasefiles/$f" -constraints dbasefiles -cleanup {
        $d forget; unset d
    } -body {
        dbf d -open [file join [testsDirectory] dbasefiles $f $p.DBF]
    } -result {dbf.????} -match glob
    unset f p
}

#                           excel          cdbf           stackoverflow
# cp1251.dbf                invald ext     Visual FoxPro
# dbase_02.dbf              unknown format ~=
# dbase_03.dbf              open           dBase III      dBase III without memo file
# dbase_03_cyrillic.dbf     open           dBase III      dBase III without memo file (UTF-8)
# dbase_8b.dbf              open           dBase IV       dBase IV with memo file
# dbase_8c.dbf              invalid ext    ~=
# dbase_30.dbf              invalid ext    Visual FoxPro  Visual FoxPro
# dbase_31.dbf              invalid ext    Visual FoxPro  Visual FoxPro with AutoIncrement field
# dbase_32.dbf              invalid ext    Visual FoxPro
# dbase_83.dbf              open           dBase III      dBase III with memo file
# dbase_83_missing_memo.dbf open           dBase III
# dbase_f5.dbf              open           FoxPro         FoxPro with memo file
# polygon.dbf               open           Invalid header

# dbase_02.dbf - invalid nHeadLen (header bytes 8,9)
# dbase_8c.dbf - invalid panFieldOffset+panFieldSize > nRecordLength

foreach {f c} {
    cp1251.dbf                    0
    dbase_02.dbf                  1
    dbase_03.dbf                  0
    dbase_03_cyrillic.dbf         0
    dbase_8b.dbf                  0
    dbase_8c.dbf                  1
    dbase_30.dbf                  0
    dbase_31.dbf                  0
    dbase_32.dbf                  0
    dbase_83.dbf                  0
    dbase_83_missing_memo.dbf     0
    dbase_f5.dbf                  0
    polygon.dbf                   0
} {
    test dbf-11.fixtures/$f "open dbf fixtures/$f" -constraints fixtures -cleanup {
        catch {$d forget; unset d}
    } -body {
        catch {dbf d -open [file join [testsDirectory] fixtures $f]}
    } -result $c
    unset f c
}

cleanupTests
